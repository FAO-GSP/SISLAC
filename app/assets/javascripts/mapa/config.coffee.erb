# Cuadro de información con título y texto
L.Control.Info = L.Control.extend({
  options:
    position: 'topleft'

  onAdd: (map) ->
    wrapper = L.DomUtil.create('div', 'info')
    title = L.DomUtil.create('h6', 'col title', wrapper)
    text = L.DomUtil.create('p', 'col text', wrapper)

    title.innerHTML = this.options.title
    text.innerHTML = this.options.text

    return wrapper

  onRemove: (map) ->
    # Nada que hacer
})

# Factory function named after the class of the control plugin to allow chaining
L.control.info = (opts) ->
  return new L.Control.Info(opts)

# exports
# https://robots.thoughtbot.com/module-pattern-in-javascript-and-coffeescript
window.Mapa = {}

# Genera el markup del popup para cada punto del GeoJSON
Mapa.preparar_markup = (perfil) ->
  serie = perfil.serie

  serie_html = if serie
    "<a target='_blank' title='Serie' href='#{serie.url}'>#{serie.nombre || 'Serie'}</a> - "
  else
    ''
  [
    serie_html,
    "<a target='_blank' title='Perfil' href='#{perfil.url}'>",
    "#{perfil.numero || 'Perfil'}",
    '</a>'
  ].join('')

# Genera el popup para cada punto del GeoJSON
Mapa.preparar_popup = (punto, capa) ->
  capa.bindPopup Mapa.preparar_markup(punto.properties)
